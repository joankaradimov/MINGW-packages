# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>
# Contributor: Luca P <meti at lplab.net>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeffrey 'jf' Lim <jfs.world@gmail.com>

_realname=tnef
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.18
pkgrel=1
arch=('any')
pkgdesc="Program which operates like tar to unpack the files inside an ms-tnef MIME attachment"
url="https://github.com/verdammelt/tnef"
license=('GPL')
depends=()
makedepends=('autoconf' 'automake-wrapper' 'make')
source=("$_realname-$pkgver.tar.gz::https://github.com/verdammelt/tnef/archive/$pkgver.tar.gz"
        "index-functions.patch")
sha256sums=('fa56dd08649f51b173017911cae277dc4b2c98211721c2a60708bf1d28839922'
            '5d4e51329ef93f9a756522818407202092baa89a36e83f7c3b9f279685f44169')

prepare() {
  cd "${srcdir}/$_realname-$pkgver"
  patch -p1 < "${startdir}"/index-functions.patch
  [ -x configure ] || autoreconf
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make
}

package() {
  cd "$srcdir"/$_realname-$pkgver
  make DESTDIR="$pkgdir" install
}
